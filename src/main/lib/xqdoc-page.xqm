xquery version "3.1";
(:
 : Copyright (c) 2019 Quodatum Ltd
 :
 : Licensed under the Apache License, Version 2.0 (the "License");
 : you may not use this file except in compliance with the License.
 : You may obtain a copy of the License at
 :
 :     http://www.apache.org/licenses/LICENSE-2.0
 :
 : Unless required by applicable law or agreed to in writing, software
 : distributed under the License is distributed on an "AS IS" BASIS,
 : WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 : See the License for the specific language governing permissions and
 : limitations under the License.
 :)
 
 (:~
 : <h1>xqdoc-links.xqm</h1>
 : <p>generate links into html docs</p>
 :
 : @author Andy Bunce
 : @version 0.1
 :)
 

module namespace page = 'quodatum:xqdoca.page';

(:~ link to module :)
declare 
function page:module($uri as xs:string,$state as map(*))                       
as element(span)
{  
 <span><a href="#todo">{ $uri }</a></span>
};

(:~ link to module :)
declare 
function page:module($file as map(*))                       
as element(span)
{  
   <span>
    <a href="{ $file?href }index.html" title="{ $file?path }">{ $file?namespace }</a> 
   </span>
};

(:~ 
 : generate standard page wrapper
 : uses $opts?resources
  :)
declare function page:wrap($body,$opts as map(*)) 
as element(html)
{
    <html>
      <head>
       <meta http-equiv="Content-Type" content="text/html; charset=UTF-8" />
       <meta name="viewport" content="width=device-width, initial-scale=1, shrink-to-fit=no"/> 
       <meta http-equiv="Generator" content="xqdoca - https://github.com/quodatum/xqdoca" />
       <!-- 
       <link rel="stylesheet" type="text/css" href="{ $opts?resources }bootstrap-4.3.1.min.css" />
        -->
        <title>
          { $opts?project } - xqDocA
        </title>
        
        <link rel="shortcut icon" type="image/x-icon" href="{ $opts?resources }xqdoc.png" />
        <link rel="stylesheet" type="text/css" href="{ $opts?resources }prism.css"/>
        <link rel="stylesheet" type="text/css" href="{ $opts?resources }page.css" />
        <link rel="stylesheet" type="text/css" href="{ $opts?resources }query.css" />
        <link rel="stylesheet" type="text/css" href="{ $opts?resources }base.css" />
     
      
      </head>

      <body class="home" id="top">
        <div id="main" >
        {$body}
        </div>
        <div class="footer">
            <p style="text-align:right">Generated by 
            <a href="https://github.com/Quodatum/xqdoca" target="_blank">xqDocA</a> 
            at {current-dateTime()}</p>
          </div>
         <script  src="{ $opts?resources }prism.js" type="text/javascript"> </script>
       
      </body>
    </html>
};

(:~ 
 : build toc 
 : @param $name title
 : @param $toc xml 
 :)
declare function page:toc2($name as xs:string,$toc as element(toc))
as element(nav)
{
    <nav id="toc">
            <h2>
                <a id="contents"></a>
                <span >
                   { $name }
                </span>
            </h2>
            <ol class="toc">{
              for $item at $pos in $toc/item
              return
                  <li>
                      <a href="{ $item/@href }">
                          <span class="secno">{ $pos }</span>
                          <span class="content">{ $item/node() }</span>
                      </a>
                  </li>   
          }</ol>
        </nav> 
};
